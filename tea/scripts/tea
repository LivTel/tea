#!/bin/csh

# ---------------------------------------------
# Launcher for: Telescope Embedded Agent (TEA).
# ---------------------------------------------

if (${#argv} < 1) then
    echo "Usage: tea ( start | stop ) <scope>"
    exit 1
endif

source /opt/ngat/etc/cshrc

source ${DEPLOY_BIN}/class_setup
source ${DEPLOY_BIN}/tea_setup

set model = $2

#source $TEA_CONFIG/${model}_setup

set PID = $DEPLOY_TMP/tea_${model}.pid

onintr cleanup

switch ($1)

    case stop:

        echo "Telescope Embedded Agent: (${model}) stopping"

        if (! -e $PID) then
            echo "Cannot locate a PID file: $PID "
            exit 1
        endif
        set AID = `cat $PID`

        if ($AID == "") then
            echo "$PID does not contain a valid PID for the TEA"
            exit 2
        endif

        set BID = `ps -ef | grep $AID | awk '{if ($3 == '$AID') print $2}'`
        echo TEA Watchdog process is $AID ..Killing with SIG_KILL
        kill -9 $AID
        echo TEA JVM Process is      $BID ..Killing with SIG_KILL
        kill -9 $BID

        rm -f $PID

        breaksw

    case start:

        echo "Telescope Embedded Agent: (${model}) starting"

        echo $$ > $PID

	set ASTRO = "-Dastrometry.impl=ngat.astrometry.TestCalculator"
	set EGD   = "-Djava.security.egd=file:/dev/urandom"
	set DEBUG = "-Djavax.net.debug=all"

	# set http proxy for RTMLParser DTD fetch
	#set HTTP_PROXY = "-Dhttp.proxyHost=wwwcache.livjm.ac.uk -Dhttp.proxyPort=8080"
	set HTTP_PROXY = ""

	# set if we are using RMI mode.
	#set REG = -"DregisterHandler=true"
	set REG

	# These are only needed if using RMI mode - make sure the registry started before TEA.
        set POLICY = "-Djava.security.policy=/occ/tmp/policy.dat"
        set SRC = "-Djava.rmi.server.codebase=file:///${TEA_CLASS}/tea.jar"

	# should really run from TEA_HOME
	cd $TEA_SCRIPT

	java -DTEA $POLICY $SRC $ASTRO $EGD $REG $HTTP_PROXY org.estar.tea.TelescopeEmbeddedAgent ${model} $TEA_CONFIG/tea.properties

	set mexit = $status
        echo "TEA (${model}) Process exited with status $mexit "

        rm -f $PID

        exit 0

        breaksw

endsw

cleanup:
rm -f $PID

